
allprojects { Project proj ->
    proj.plugins.withId('java') {
        logger.debug("[gradlew] - Adding integTest task to ${proj.name}")

        proj.sourceSets {
            integTest {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
        }

        proj.configurations {
            integTestCompile.extendsFrom testCompile
            integTestRuntime.extendsFrom testRuntime
        }

        task integTest(type: Test, group: 'Verification') {
            reports {
                html.destination = "${proj.testReportDir}/integTest"
                junitXml.destination = "${proj.testResultsDir}/integTest"
            }
            classpath = sourceSets.integTest.runtimeClasspath
            testClassesDir = sourceSets.integTest.output.classesDir

            beforeTest { testName ->
                logger.info("Running test: ${testName}")
            }

            doFirst {
                if(proj.files(sourceSets.integTest.getAllSource()).isEmpty()) {
                    logger.lifecycle("${proj.name} does not contain any integration tests")
                }
            }
        }

        proj.plugins.withId('idea') {
            proj.afterEvaluate {
                idea {
                    module {
                        testSourceDirs += sourceSets.integTest.java.srcDirs
                        scopes.TEST.plus.add(configurations.integTestRuntime)
                        scopes.TEST.plus.add(configurations.integTestCompile)
                    }
                }
            }
        }

        proj.plugins.withId('eclipse') {
            eclipse {
                classpath {
                    plusConfigurations.add(configurations.integTestCompile)
                    plusConfigurations.add(configurations.integTestRuntime)
                }
            }
        }
    }
}
